# Maintainer: G. Richard Bellamy <rbellamy@pteradigm.com>
# Contributor: Andrea Repetto <andrea.repetto85@gmail.com>
# Contributor: Bogdan Szczurek <thebodzio@gmail.com>

pkgname=flexget-git
pkgver=1.2.125
pkgrel=1

pkgdesc="Automate downloading or processing content (torrents, podcasts, etc.)"

arch=('i686' 'x86_64')
url="http://flexget.com/"
license=('MIT')

depends=('python2'
         # documented in FlexGet.egg-info/requires.txt
         'python2-feedparser>=5.1.3'
         'python2-sqlalchemy>=0.9.1'
         'python2-yaml'
         'python2-beautifulsoup4<4.4'
         'python2-html5lib>=0.11'
         'python2-pyrss2gen' #AUR#
         'python2-progressbar'
         'python2-rpyc' #AUR#
         'python2-jinja'
         'python2-requests>=1.0'
         'python2-dateutil1=2.1' #AUR#
         'python2-jsonschema>=2.0' #AUR#
         'python2-tvrage' #AUR#
         'python2-tmdb3' #AUR#
         # undocumented in FlexGet.egg-info/requires.txt
         )
optdepends=('python2-guppy: for memusage plugin' #AUR#
            'python2-transmissionrpc: Transmission support' #AUR#
            'python2-cherrypy: web interface'
            'python2-flask: web interface'
            'python2-pynzb: nzb support')
makedepends=('git'
             'python2-distribute'
             'python2-paver')

provides=('flexget')
conflicts=('flexget')

source=('git://github.com/Flexget/Flexget'
        'flexget.service')
sha256sums=('SKIP'
            '5fe1f70265744e1ebd5e91fe7d1e83617ac9454928de7fa0ad11107745225513')

pkgver() {
  cd Flexget
  git describe | sed 's/^v//'
}

prepare() {
  cd Flexget

  msg "Patching shebangs"
  sed -i 's/\(python\)/\12/' flexget{,/ui}/__init__.py
}

package() {
  cd Flexget

  # Python setup
  python2 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=1 || return 1

  # License
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

  # install systemd service
  install -Dm644 ../flexget.service "${pkgdir}"/usr/lib/systemd/user/flexget.service
}

# vim:set ts=2 sw=2 et:
